#!/usr/bin/env escript
%% -*- erlang -*-
%%! -smp enable -name rtmp_bench -mnesia debug verbose -pa ebin -pa ../erlang-amf/ebin
-mode(compile).

main([]) ->
  io:format("rtmp_bench url [count]~n"),
  ok;

main([Path]) ->
  main([Path, "10"]);
  
main([URL, CountS]) ->
  {ok, Re} = re:compile("([^:]+)://([^/]+)/(.*)"),
  process_flag(trap_exit, true),
  {match, [_, _Protocol, Server, Path]} = re:run(URL, Re, [{capture, all, list}]),
  Count = list_to_integer(CountS),
  start_spawner(Server, list_to_binary(Path), Count, 0).
  
start_spawner(Server, Path, Count, Number) when Number < Count ->
  spawn_link(fun() -> init_rtmp_client(Server, Path) end),
  start_spawner(Server, Path, Count, Number + 1);

start_spawner(Server, Path, Count, Count) ->
  receive
    {'EXIT', _Pid, _Reason} ->
      start_spawner(Server, Path, Count, Count - 1);
    Else ->
      io:format("Spawner message: ~p~n", [Else]),
      start_spawner(Server, Path, Count, Count)
  end.
  
init_rtmp_client(Server, Path) ->
  {ok, Socket} = gen_tcp:connect(Server, 1935, [binary, {active, false}, {packet, raw}]),
  {ok, RTMP} = rtmp_socket:connect(Socket),
  io:format("Connected to ~s~n", [Server]),
  rtmp_client(RTMP, Path).
  
rtmp_client(RTMP, Path) ->
  receive 
    {rtmp, RTMP, connected} ->
      play(RTMP, Path),
      rtmp_client(RTMP, Path);
    {rtmp, RTMP, Message} when element(1, Message) == rtmp_message ->
      rtmp_client(RTMP, Path);
    Else ->
      io:format("Client message: ~p (~p)~n", [Else, RTMP]),
      rtmp_client(RTMP, Path)
  after
    10000 ->
      io:format("Client timeout~n"),
      ok
  end.
    

play(RTMP, Path) -> 
  Connect = rtmp_lib:connect(RTMP),
  Stream = rtmp_lib:createStream(RTMP),
  rtmp_lib:play(RTMP, Stream, Path),
  io:format("Playing ~s~n", [Path]).
  
  